# The MIT License (MIT)
#
# Copyright (c) 2021-2022 Max Trunnikov
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

+alias org.eolang.math.nan
+alias org.eolang.math.negative-infinity
+alias org.eolang.math.positive-infinity
+alias org.eolang.switch
+home https://github.com/objectionary/eo-math
+package org.eolang.math
+rt jvm org.eolang:eo-math:0.0.0
+version 0.0.0

[n] > number
  # Checking type of the number's decorator.
  # If $.n is int, return true, otherwise false.
  [] > is-int
    ^.n > value!
    if. > @
      value.eq 0
      TRUE
      if.
        value.eq 0.0
        FALSE
        eq.
          div.
            value
            value
          1

  # Checking type of the number's decorator.
  # If $.n is float, return true, otherwise false.
  [] > is-float
    ^.n > value!
    if. > @
      value.eq 0.0
      TRUE
      if.
        value.eq 0
        FALSE
        eq.
          div.
            value
            value
          1.0

  # Checking if number is NaN
  [] > is-nan
    eq. > @
      ^.n.as-bytes
      7F-C0-00-00

  # Checking if number is infinite
  [] > is-infinite
    or. > @
      eq.
        ^.n > value!
        positive-infinity
      eq.
        value
        negative-infinity

  [x] > xor
    if. > @
      is-int
      as-int.
        xor.
          ^.n.as-bytes
          x.as-bytes
      error "xor cannot be applied to float"

  [x] > or
    if. > @
      is-int
      as-int.
        or.
          ^.n.as-bytes
          x.as-bytes
      error "or cannot be applied to float"

  [x] > and
    if. > @
      is-int
      as-int.
        and.
          ^.n.as-bytes
          x.as-bytes
      error "and cannot be applied to float"

  # Calculate bitwise left shift
  [x] > left
    if. > @
      is-int
      as-int.
        left.
          ^.n.as-bytes
          x
      error "left cannot be applied to float"

  # Calculate bitwise right shift
  [x] > right
    if. > @
      is-int
      as-int.
        right.
          ^.n.as-bytes
          x
      error "right cannot be applied to float"

  # Tests that $.n â‰  x
  [x] > neq
    (^.n.eq x).not > @

  # Calculate MOD
  [x] > mod
    ^.n > dividend!
    x > divisor!

    if. > @
      divisor.eq 0
      error "mod by zero"
      if.
        dividend.gt 0
        abs-mod
        neg.
          abs-mod

    [] > abs-mod
      (number dividend).abs > int-abs!
      (number divisor).abs > x-abs!
      minus. > @
        int-abs
        x-abs.times
          int-abs.div x-abs

  # Absolute value of $.n (i.e., with no sign)
  [] > abs
    ^.n > value!
    if. > @
      is-int
      if.
        value.gte 0
        value
        value.neg
      if.
        value.gte 0.0
        value
        value.neg

  # Round ^.n up
  [] > ceil
    ^.n > value!
    if. > @
      or.
        is-nan
        is-int
        eq.
          value
          positive-infinity
        eq.
          value
          negative-infinity
      value
      []
        as-int > self-as-int!
        if. > @
          value.lte 0.0
          self-as-int
          self-as-int.plus 1

  # Make ^.n power x
  [x] > pow /?

  # Make ^.n power x
  # @todo #21:30 min. Add an ability to raise a number to float power.
  #  `exp` and `ln` objects are needed.  sqr(n,x) = exp(n * ln(x))
  #
  # @todo #21:30 min. Improve performance to be able to work with big numbers.
  #  Should use binary exponentiation. It allows to raise any number to the n'th
  #  power in O(log n) multiplications
  [x] > power
    as-float > self-as-float!

    and. > both-are-int!
      (number x).is-int
      is-int

    if. > x-as-float!
      (number x).is-nan > x-is-nan!
      nan
      (number x).as-float

    [y] > powf
      if. > @
        y.gt 0.0
        times.
          self-as-float
          ^.powf (y.minus 1.0)
        1.0

    [y] > powi
      if. > @
        y.gt 0
        times.
          ^.^.n
          ^.powi (y.minus 1)
        1

    switch > @
      *
        or.
          x-is-nan
          is-nan
        nan
      *
        x-as-float.eq 0.0
        if.
          both-are-int
          1
          1.0
      *
        x-as-float.lt 0.0
        if.
          self-as-float.eq 0.0
          positive-infinity
          div.
            1.0
            powf
              neg.
                x-as-float
      *
        x-as-float.gt 0.0
        if.
          self-as-float.eq 0.0
          if.
            both-are-int
            0
            0.0
          if.
            both-are-int
            powi x
            powf x-as-float

  # Converts ^.n to float
  [] > as-float /float

  # Converts this to int
  [] > as-int /int

  # Converts ^.n to string
  [] > as-string /string

  # Sign of $ (-1, 0, or 1) if $ is int
  # or (-1.0, 0.0, or 1.0) if $ is float
  [] > signum
    if. > @
      is-int
      if.
        ^.n.lt 0
        -1
        if.
          ^.n.eq 0
          0
          1
      if.
        ^.n.lt 0.0
        -1.0
        if.
          ^.n.eq 0.0
          0.0
          1.0

  # @todo #56:90 min. Implement sin using this taylor object.
  #  You need to code function for coefficient of nth term.
  #  Accuracy can be decreased because of stack limitation.

  # @todo #56:90 min. Implement cos using this taylor object.
  #  You need to code function for coefficient of nth term.
  #  Accuracy can be decreased because of stack limitation.

  # @todo #56:90 min. Implement tg using this taylor object.
  #  You need to code function for coefficient of nth term.
  #  Accuracy can be decreased because of stack limitation.

  # @todo #56:90 min. Implement exp using this taylor object.
  #  You need to code a function for coefficient of nth term.
  #  Accuracy can be decreased because of stack limitation.
  [] > exp
    times. > @
      exp-x0
      exp-x1
    (number n).as-float > value!
    0.69314718055994530941 > ln-2!
    as-int. > m!
      number
        div.
          value
          number.exp.ln-2
    15000000.0 > count!
    pow. > exp-x0!
      (number 2.0)
      (number m).as-float
    pow. > exp-x1!
      number
        plus.
          1.0
          div.
            minus.
              value
              times.
                (number m).as-float
                ln-2
            count
      count

  # Computes series buy the numbers for start and finish and function of
  # coefficient of nth term. It is NaN if infinity passed because the
  # implementation would get infinity time to calculate sum of infinity terms
  [start finish f] > taylor
    ^.n > value!
    memory start > iterator
    memory > product
      value.minus value
    memory > current-power
      power start
    number > distance!
      finish.minus start
    if. > @
      or.
        is-nan
        is-infinite
        distance.is-nan
        distance.is-infinite
      nan
      while.
        lt.
          iterator
          finish
        [i]
          seq > @
            iterator.write
              iterator.plus 1
            product.write
              plus.
                product
                times.
                  f
                    i
                  current-power
            current-power.write
              current-power.times value
            product

  # Take logarithm of ^.n
  # @todo #53:30min. Remove unnecessary checks and move some of them to ln
  #  when ln is implemented in EO and Double.NaN, Double.POSITIVE_INFINITY,
  #  Double.NEGATIVE_INFINITY are converted to eolang objects.
  [base] > log
    number base > base-as-number!

    if. > @
      or.
        is-nan
        base-as-number.is-nan
      nan
      []
        if. > base-as-float!
          base-as-number.is-infinite
          base
          base-as-number.as-float

        if. > self-as-float!
          is-infinite
          ^.^.n
          as-float

        switch > @
          *
            or.
              self-as-float.lt 0.0
              base-as-float.lt 0.0
              and.
                eq. > self-is-inf!
                  positive-infinity
                  self-as-float
                or. > base-is-inf-or-zero!
                  positive-infinity.eq base-as-float
                  base-as-float.eq 0.0
              and.
                self-as-float.eq 0.0 > self-is-zero!
                base-is-inf-or-zero
              and.
                self-as-float.eq 1.0 > self-is-one!
                base-as-float.eq 1.0 > base-is-one!
            nan
          *
            and.
              self-is-inf
              base-is-one
            positive-infinity
          *
            and.
              self-is-zero
              base-is-one
            negative-infinity
          *
            and.
              self-is-one
              base-is-inf-or-zero
            0.0
          *
            TRUE
            div.
              (number self-as-float).ln
              (number base-as-float).ln

  # Natural logarithm of ^.n
  # @todo #53:30min. Natural logarithm should be implemented in EO when there
  #  is an object or an atorm that can calculate a series.
  [] > ln /float
